//

uniform sampler2D indexTex;
uniform sampler2D cacheTex;
uniform float tileSize;
uniform float indexSize;
uniform float cacheTexSize;
uniform float virtualSize;
uniform float maxLod;
uniform float border;
uniform float padTileSize;

const float eps = 0.00001;

float calcLod(float2 dx, float2 dy)
{
  float2 d = sqrt(dx*dx + dy*dy);
  float lo = min(d.x, d.y);
  float hi = max(d.x, d.y);
  float md = 0.5 * (lo + hi);
  float lod = log2(md*virtualSize) + 0.7; // lod bias
  return clamp(lod, 0, maxLod-eps);
}

float4 vtexFetchLod(float2 texCoord, float lod, float2 dx, float2 dy)
{
  float4 tileData = tex2Dlod(indexTex, float4(texCoord, 0, lod));
  while (tileData.w == 0)
    tileData = tex2Dlod(indexTex, float4(texCoord, 0, ++lod));
  float2 tileIdx = floor(tileData.xy * 255.0f + 0.5f);
  float tileScale = exp2(floor(tileData.z * 255.0f + 0.5f));
  float2 posInTile = frac(texCoord * indexSize / tileScale);

  float2 posInCache = (tileIdx * padTileSize + border + posInTile*tileSize) / cacheTexSize;

  float dcoef = virtualSize / cacheTexSize / tileScale;
  return tex2D(cacheTex, posInCache, dx*dcoef, dy*dcoef);
}

float4 vtexFetch(float2 texCoord, float2 dx, float2 dy)
{
  float lod = calcLod(dx, dy);
  float hiLod = floor(lod);
  float loLod = hiLod + 1;
  float4 c1 = vtexFetchLod(texCoord, hiLod, dx, dy);
  float4 c2 = vtexFetchLod(texCoord, loLod, dx, dy);
  return lerp(c1, c2, lod - hiLod);
}
